AppType=JavaFX
Build1=Default,b4j.example
File1=main.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=javaobject
Library4=jshell
Library5=apachestringutilsb4j
NumberOfFiles=1
NumberOfLibraries=5
NumberOfModules=0
Version=6.01
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form

	Private Button1 As Button
	Private Button2 As Button
	Private Button3 As Button
	Private Label2 As Label
	Private ListView1 As ListView
	Private tesseractInstalled As Boolean
	Private langmap As Map
	Private ComboBox1 As ComboBox
	Private TextArea1 As TextArea
	Private TextField1 As TextField
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("main") 'Load the layout file.
	MainForm.Show
	Dim sh As Shell
	sh.Initialize("sh","tesseract",Null)
	sh.run(10000)

	langmap.Initialize
	Dim langlist As List
	langlist=File.ReadList(File.DirApp,"lang.conf")
	For i=0 To langlist.Size-1 Step 2
		langmap.Put(langlist.Get(i),langlist.Get(i+1))
		ComboBox1.Items.Add(langlist.Get(i))
	Next
	ComboBox1.SelectedIndex=0
	'scan("file-"&66&".jpg",66)
	TextArea1.Text=""
End Sub

'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
	Return True
End Sub

Sub ListView1_MouseClicked (EventData As MouseEvent)
	
End Sub

Sub Button3_MouseClicked (EventData As MouseEvent)
	
	For Each line As String In ListView1.Items
		Dim list1 As List
		list1.Initialize
		list1.AddAll(Regex.Split("	",line))
		Dim chaptername As String
		Dim startnum,endnum As Int
		chaptername=list1.Get(0)
		startnum=list1.Get(1)
		endnum=list1.Get(2)
		Dim content As String
		For i=startnum To endnum
			Dim pdfnum As Int
			pdfnum=i+TextField1.Text
			'scan("file-"&i&".jpg",i)
			TextArea1.Text=TextArea1.Text&i&CRLF
			Dim args As List
			args.Initialize
			args.AddAll(Array As String("file"&pdfnum&".jpg",pdfnum,"-l",langmap.Get(ComboBox1.Value)))
			Dim sh1 As Shell
			Log(File.Exists(File.Combine(File.DirApp,"tesseract-ocr"),"tesseract.exe"))
			If tesseractInstalled Then
				sh1.Initialize("sh1","tesseract",args)
			Else
				sh1.Initialize("sh1",File.Combine(File.Combine(File.DirApp,"tesseract-ocr"),"tesseract"),args)
			End If
	        
			sh1.WorkingDirectory = File.Combine(File.DirApp,"temp")
			sh1.Run(100000)
			wait for sh1_ProcessCompleted (Success As Boolean, ExitCode As Int, StdOut As String, StdErr As String)
			
			If Success And ExitCode = 0 Then
				Log("Success")
				Log(StdOut)
			Else
				Log("Error: " & StdErr)
			End If
			content=content&"Page "&i&CRLF&CRLF&removeLines(File.ReadString(File.Combine(File.DirApp,"temp"),pdfnum&".txt"))
		Next
		File.WriteString(File.DirApp,chaptername&".txt",content)
	Next
End Sub

Sub Button2_MouseClicked (EventData As MouseEvent)
	Dim path As String
	Dim fc As FileChooser
	fc.Initialize
	fc.SetExtensionFilter("Tab分割文本",Array As String("*.txt"))
	path=fc.ShowOpen(MainForm)
	If path="" Then
		Return
	End If
	Dim contents As String
	contents=File.ReadString(path,"")
	ListView1.Items.AddAll(Regex.Split(CRLF,contents))
End Sub

Sub Button1_MouseClicked (EventData As MouseEvent)
	Dim path As String
	Dim fc As FileChooser
	fc.Initialize
	fc.SetExtensionFilter("pdf",Array As String("*.pdf"))
	path=fc.ShowOpen(MainForm)
	If path="" Then
		Return
	End If
	Label2.Text=path
	File.Copy(path,"",File.Combine(File.DirApp,"temp"),"file.pdf")
	TextArea1.Text="导出PDF为图片到临时文件夹中。。。"
	Dim sh As Shell
	sh.Initialize("sh3",File.Combine(File.Combine(File.DirApp,"jre1.8.0_181\bin"),"java"),Array As String("-Xmx1024m","-jar","pdfbox-app.jar","PDFToImage", File.Combine(File.Combine(File.DirApp,"temp"),"file.pdf")))
	sh.WorkingDirectory = File.Combine(File.DirApp,"tesseract-ocr")
	sh.run(-1)
	wait for sh3_ProcessCompleted (Success As Boolean, ExitCode As Int, StdOut As String, StdErr As String)
	TextArea1.Text=TextArea1.Text&StdOut
	If Success And ExitCode = 0 Then
		Log("Success")
		Log(StdOut)
		TextArea1.Text="导出完成"
	Else
		Log("Error: " & StdErr)
		TextArea1.Text="导出失败"
	End If
End Sub


Sub sh_ProcessCompleted (Success As Boolean, ExitCode As Int, StdOut As String, StdErr As String)
	If Success And ExitCode = 0 Then
		Log("Success")
		Log(StdOut)
		tesseractInstalled=True
	Else
		Log("Error: " & StdErr)
		tesseractInstalled=False
	End If
End Sub

Sub scan(filename As String,outputname As String)
	Dim args As List
	args.Initialize
	args.AddAll(Array As String(filename,outputname,"-l",langmap.Get(ComboBox1.Value)))
	Dim sh1 As Shell
	Log(File.Exists(File.Combine(File.DirApp,"tesseract-ocr"),"tesseract.exe"))
	If tesseractInstalled Then
		sh1.Initialize("sh1","tesseract",args)
	Else
		sh1.Initialize("sh1",File.Combine(File.Combine(File.DirApp,"tesseract-ocr"),"tesseract"),args)
	End If
	
	sh1.WorkingDirectory = File.Combine(File.DirApp,"temp")
	sh1.Run(100000)
	wait for sh1_ProcessCompleted (Success As Boolean, ExitCode As Int, StdOut As String, StdErr As String)
	TextArea1.Text=TextArea1.Text&StdOut
	If Success And ExitCode = 0 Then
		Log("Success")
		Log(StdOut)
	Else
		Log("Error: " & StdErr)
	End If
End Sub


Sub removeLines(input As String) As String
	Dim result As String
	Dim list1 As List
	list1.Initialize
	list1.AddAll(Regex.Split(CRLF,input))
	Log(list1)
	For Each line As String In list1
		Log(line)
		If line="" Or line=" " Then
			Continue
		End If
		If isSentenceEnd(line)=False Then
			result=result&" "&line
		Else
			result=result&" "&line&CRLF
		End If
	Next

	Dim list2 As List
	list2.Initialize
	list2.AddAll(Regex.Split(CRLF,result))
	result=""
	For Each line As String In list2
		result=result&line.Trim&CRLF&CRLF
	Next
    Return result
End Sub

Sub isSentenceEnd(line As String) As Boolean
	If line.EndsWith("。") Or line.EndsWith("！") Or line.EndsWith("？") Or line.EndsWith("”") Or line.EndsWith(".") Or line.EndsWith("!") Or line.EndsWith("?") Or line.EndsWith(";") Or line.EndsWith(Chr(34)) Then
		Return True
	Else
		Return False
	End If
End Sub
